; The following code is largely based on
; https://www.cs.berkeley.edu/~bh/ssch18/trees.html
(define-syntax infix
  (syntax-rules ()
    ((infix x)
      (infix:compute (infix:parse 'x)))
    ((infix x ...)
      (infix x))))

(define (infix:parse expr)
  (infix:_parse-helper expr '() '()))

(define (infix:_parse-helper expr operators operands)
  (cond ((null? expr)
	 (if (null? operators)
	     (car operands)
	     (infix:_handle-op '() operators operands)))
	((number? (car expr))
	 (infix:_parse-helper (cdr expr)
		       operators
		       (cons (tree:make-node (car expr)) operands)))
	((list? (car expr))
	 (infix:_parse-helper (cdr expr)
		       operators
		       (cons (infix:parse (car expr)) operands)))
	(else (if (or (null? operators) (> (infix:precedence (car expr))
			 (infix:precedence (car operators))))
		  (infix:_parse-helper (cdr expr)
				(cons (car expr) operators)
				operands)
		  (infix:_handle-op expr operators operands)))))

(define (infix:_handle-op expr operators operands)
  (infix:_parse-helper expr
		(cdr operators)
		(cons (tree:make-tree (car operators)
				 (list (cadr operands) (car operands)))
		      (cddr operands))))

(define (infix:precedence oper)
  (cond
    ((list:in? '(+ -) oper) 1)
    ((eq? '^ oper) 3)
    (else 2)))

(define (infix:compute tree)
  (if (number? (tree:get-datum tree))
      (tree:get-datum tree)
      ((infix:_function-named-by (tree:get-datum tree))
         (infix:compute (car (tree:get-children tree)))
         (infix:compute (cadr (tree:get-children tree))))))

(define (infix:_function-named-by oper)
  (cond ((equal? oper '+) +)
	((equal? oper '-) -)
	((equal? oper '*) *)
	((equal? oper '^) **)
	((equal? oper '/) /)
	(else (error "no such operator as" oper))))

(define-struct tree (datum children))

(define (tree:make-node datum)
  (tree:make-tree datum []))
